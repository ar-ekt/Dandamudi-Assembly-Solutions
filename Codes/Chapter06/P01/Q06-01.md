
### Q06.01

What modifications would you make to the insertion sort procedure discussed in Section 6.3 to sort the array in descending order? Make the necessary modifications to the program and test it for correctness.

#### 6.3 Illustrative Examples

We now present two examples to illustrate the usefulness of the various addressing modes. The first example sorts an array of integers using the insertion sort algorithm, and the other example implements a binary search to locate a value in a sorted array.

##### Example 6.1 Sorting an integer array using the insertion sort.

This example requests a set of integers from the user and displays these numbers in sorted order. The main procedure reads a maximum of `MAX_SIZE` integers (lines 20 to 28). It accepts only nonnegative numbers. Entering a negative number terminates the input (lines 24 and 25).
The main procedure passes the array pointer and its size (lines 30 to 34) to the insertion sort procedure. The remainder of the main procedure displays the sorted array returned by the sort procedure. Note that the main procedure uses the indirect addressing mode on lines 26 and 41.
The basic principle behind the insertion sort is simple: insert a new number into the sorted array in its proper place. To apply this algorithm, we start with an empty array. Then insert the first number. Now the array is in sorted order with just one element. Next insert the second number in its proper place. This results in a sorted array of size two. Repeat this process until all the numbers are inserted. The pseudocode for this algorithm, shown below, assumes that the array index starts with 0:
``` 
insertion_sort (array, size)
	for (i = 1 to size−1)
		temp := array[i]
		j := i − 1
		while ((temp < array[j]) AND (j ≥ 0))
			array[j+1] := array[j]
			j := j − 1
		end while
		array[j+1] := temp
	end for
end insertion_sort
```
Here, index i points to the number to be inserted. The array to the left of i is in sorted order. The numbers to be inserted are the ones located at or to the right of index i. The next number to be inserted is at i. The implementation of the insertion sort procedure, shown in Program 6.1, follows the pseudocode.

##### Program 6.1 Insertion sort
```assembly
01: ;TITLE Sorting an array by insertion sort INS_SORT.ASM
02: ;
03: ;		Objective: To sort an integer array using insertion sort.
04: ; 		    Input: Requests numbers to fill array.
05: ;		   Output: Displays sorted array.
06: %include "io.mac"
07:
08: .DATA
09: MAX_SIZE 	 EQU 100
10: input_prompt db "Please enter input array: "
11:		 db "(negative number terminates input)",0
12: out_msg 	 db "The sorted array is:",0
13:
14: .UDATA
15: array 	 resd MAX_SIZE
16:
17: .CODE
18: 	.STARTUP
19: 	PutStr input_prompt			; request input array
20: 	mov EBX,array
21: 	mov ECX,MAX_SIZE
22: array_loop:
23: 	GetLInt EAX 				; read an array number
24: 	cmp EAX,0 				; negative number?
25: 	jl exit_loop 				; if so, stop reading numbers
26: 	mov [EBX],EAX 				; otherwise, copy into array
27: 	add EBX,4 				; increment array address
28:	loop array_loop 			; iterates a maximum of MAX_SIZE
29: exit_loop:
30: 	mov EDX,EBX 				; EDX keeps the actual array size
31: 	sub EDX,array 				; EDX = array size in bytes
32: 	shr EDX,2	 			; divide by 4 to get array size
33: 	push EDX 				; push array size & array pointer
34: 	push array
35: 	call insertion_sort
36: 	PutStr out_msg 				; display sorted array
37: 	nwln
38: 	mov ECX,EDX
39: 	mov EBX,array
40: display_loop:
41: 	PutLInt [EBX]
42: 	nwln
43: 	add EBX,4
44: 	loop display_loop
45: done:
46: 	.EXIT
47:
48: ;-----------------------------------------------------------
49: ; This procedure receives a pointer to an array of integers
50: ; and the array size via the stack. The array is sorted by
51: ; using insertion sort. All registers are preserved.
52: ;-----------------------------------------------------------
53: %define SORT_ARRAY EBX
54: insertion_sort:
55: 	pushad 					; save registers
56: 	mov EBP,ESP
57: 	mov EBX,[EBP+36] 			; copy array pointer
58: 	mov ECX,[EBP+40] 			; copy array size
59: 	mov ESI,4 				; array left of ESI is sorted
60: for_loop:
61: 	; variables of the algorithm are mapped as follows.
62: 	; EDX = temp, ESI = i, and EDI = j
63: 	mov EDX,[SORT_ARRAY+ESI] 	; temp = array[i]
64: 	mov EDI,ESI 			; j = i-1
65: 	sub EDI,4
66: while_loop:
67: 	cmp EDX,[SORT_ARRAY+EDI] 	; temp < array[j]
68: 	jge exit_while_loop
69: 	; array[j+1] = array[j]
70: 	mov EAX,[SORT_ARRAY+EDI]
71: 	mov [SORT_ARRAY+EDI+4],EAX
72: 	sub EDI,4 			; j = j-1
73: 	cmp EDI,0 			; j >= 0
74: 	jge while_loop
75: exit_while_loop:
76: 	; array[j+1] = temp
77: 	mov [SORT_ARRAY+EDI+4],EDX
78: 	add ESI,4 			; i = i+1
79: 	dec ECX
80: 	cmp ECX,1 			; if ECX = 1, we are done
81: 	jne for_loop
82: sort_done:
83: 	popad 				; restore registers
84: 	ret 8
```
Since the sort procedure does not return any value to the main program in registers, we can use `pushad` (line 55) and `popad` (line 83) to save and restore registers. As `pushad` saves all eight registers on the stack, the offset is appropriately adjusted to access the array size and array pointer parameters (lines 57 and 58).
The `while` loop is implemented by lines 66 to 75, and the for loop is implemented by lines 60 to 81. Note that the array pointer is copied to the EBX (line 57), and line 53 assigns a convenient label to this. We have used the based-indexed addressing mode on lines 63, 67, and 70 without any displacement and on lines 71 and 77 with displacement. Based addressing is used on lines 57 and 58 to access parameters from the stack.
