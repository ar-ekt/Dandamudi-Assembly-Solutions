### Q9.2

Modify the octal-to-binary conversion program shown in Program 9.2 to include error checking for nonoctal input. For example, digit 8 in the input should be flagged as an error. In case of error, terminate the program.

#### **Example 9.13** *Conversion from octal to binary.*

An algorithm for converting octal numbers to binary is given in Appendix A. The main program is similar to that in the last example. The procedure `to_binary` receives an octal number as a character string via BX and the 8-bit binary value is returned in AL. The pseudocode of this procedure is as follows:
```
to_binary (octal string)
	binary value := 0
	for (i = 0 to 3)
		if (octal string[i] = NULL)
			goto finished
		end if
		digit := numeric(octal string[i])
		binary value := binary value * 8 + digit
	end for
finished:
end to_binary
```
The function `numeric` converts a digit character to its numeric equivalent. The program is shown in Program 9.2. Note that we use the `shl` instruction to multiply by 8 (line 54). The rest of the code follows the pseudocode.

##### **Program 9.2** Octal-to-binary conversion
```assembly
 1:   ;Octal-to-binary conversion using shifts OCT_BIN.ASM
 2:   ;
 3:   ;			Objective: To convert an 8-bit octal number to the
 4:   ; 			   binary equivalent using shift instruction.
 5:   ; 		    Input: Requests an 8-bit octal number from user.
 6:   ; 		   Output: Prints the decimal equivalent of the input
 7:   ; 			   octal number.
 8:   %include "io.mac"
 9:
10:   .DATA
11:   input_prompt	db	’Please input an octal number: ’, 0
12:   out_msg1		db	’The decimal value is: ’, 0
13:   query_msg		db	’Do you want to quit (Y/N): ’, 0
14:
15:   .UDATA
16:   octal_number	resb	4	; to store octal number
17:
18:   .CODE
19:   .STARTUP
20:   read_input:
21:   PutStr input_prompt		; request an octal number
22:   GetStr octal_number, 4		; read input number
23:   mov EBX, octal_number		; pass octal # pointer
24:   call to_binary			; returns binary value in AX
25:   PutStr out_msg1
26:   PutInt AX				; display the result
27:   nwln
28:   PutStr query_msg			; query user whether to terminate
29:   GetCh AL				; read response
30:   cmp AL, ’Y’			; if response is not ’Y’
31:   jne read_input			; read another number
32:   done:				; otherwise, terminate program
33:   .EXIT
34:
35:   ;-----------------------------------------------------------
36:   ; to_binary receives a pointer to an octal number string in
37:   ; EBX register and returns the binary equivalent in AL (AH is
38:   ; set to zero). Uses SHL for multiplication by 8. Preserves
39:   ; all registers, except AX.
40:   ;-----------------------------------------------------------
41:   to_binary:
42:   push EBX				; save registers
43:   push CX
44:   push DX
45:   xor EAX, EAX			; result = 0
46:   mov CX, 3				; max. number of octal digits
47:   repeat1:
48:   ; loop iterates a maximum of 3 times;
49:   ; but a NULL can terminate it early
50:   mov DL, [EBX]			; read the octal digit
51:   cmp DL, 0				; is it NULL?
52:   je finished			; if so, terminate loop
53:   and DL, 0FH			; else, convert char. to numeric
54:   shl AL, 3				; multiply by 8 and add to binary
55:   add AL, DL
56:   inc EBX				; move to next octal digit
57:   dec CX				; and repeat
58:   jnz repeat1
59:   finished:
60:   pop DX				; restore registers
61:   pop CX
62:   pop EBX
63:   ret
```
