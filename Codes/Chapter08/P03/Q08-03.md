### Q8.3

In the last exercise, you assumed that the user supplies data in sorted order. In this exercise, remove this restriction on the input data. Instead, use the selection sort procedure given in Program 8.2 to sort the array after reading the input data.

#### Last Exercise (Q8.2)

Suppose we are given a sorted array of integers. Further assume that the array is sorted in ascending order. Then we can modify the linear search procedure to search for a number S so that it stops searching either when S is found or when a number greater than S is found. Modify the linear search program shown in Program 8.1 to work on a sorted array. For this exercise, assume that the user supplies the input data in sorted order.

#### Program 8.1 Linear search of an integer array

```assembly
1: ;Linear search of integer array LIN_SRCH.ASM
2: ;
3: ; Objective: To implement linear search of an integer
4: ; array; demonstrates the use of loopne.
5: ; Input: Requests numbers to fill array and a
6: ; number to be searched for from user.
7: ; Output: Displays the position of the number in
8: ; the array if found; otherwise, not found
9: ; message.
10: %include "io.mac"
11:
12: MAX_SIZE EQU 100
13:
14: .DATA
15: input_prompt db "Please enter input array: "
16: db "(negative number terminates input)",0
17: query_number db "Enter the number to be searched: ",0
18: out_msg db "The number is at position ",0
19: not_found_msg db "Number not in the array!",0
20: query_msg db "Do you want to quit (Y/N): ",0
21:
22: .UDATA
23: array resw MAX_SIZE
24:
25: .CODE
26: .STARTUP
27: PutStr input_prompt ; request input array
28: mov EBX,array
29: mov ECX,MAX_SIZE
30: array_loop:
31: GetInt AX ; read an array number
32: cmp AX,0 ; negative number?
33: jl exit_loop ; if so, stop reading numbers
34: mov [EBX],AX ; otherwise, copy into array
35: inc EBX ; increment array address
36: inc EBX
37: loop array_loop ; iterates a maximum of MAX_SIZE
38: exit_loop:
39: mov EDX,EBX ; EDX keeps the actual array size
40: sub EDX,array ; EDX = array size in bytes
41: sar EDX,1 ; divide by 2 to get array size
42: read_input:
43: PutStr query_number ; request number to be searched for
44: GetInt AX ; read the number
45: push AX ; push number, size & array pointer
46: push EDX
47: push array
48: call linear_search
49: ; linear_search returns in AX the position of the number
50: ; in the array; if not found, it returns 0.
51: cmp AX,0 ; number found?
52: je not_found ; if not, display number not found
53: PutStr out_msg ; else, display number position
54: PutInt AX
55: jmp SHORT user_query
56: not_found:
57: PutStr not_found_msg
58: user_query:
59: nwln
60: PutStr query_msg ; query user whether to terminate
61: GetCh AL ; read response
62: cmp AL,’Y’ ; if response is not ’Y’
63: jne read_input ; repeat the loop
64: done: ; otherwise, terminate program
65: .EXIT
66:
67: ;-----------------------------------------------------------
68: ; This procedure receives a pointer to an array of integers,
69: ; the array size, and a number to be searched via the stack.
70: ; If found, it returns in AX the position of the number in
71: ; the array; otherwise, returns 0.
72: ; All registers, except EAX, are preserved.
73: ;-----------------------------------------------------------
74: linear_search:
75: enter 0,0
76: push EBX ; save registers
77: push ECX
78: mov EBX,[EBP+8] ; copy array pointer
79: mov ECX,[EBP+12] ; copy array size
80: mov AX,[EBP+16] ; copy number to be searched
81: sub EBX,2 ; adjust index to enter loop
82: search_loop:
83: add EBX,2 ; update array index
84: cmp AX,[EBX] ; compare the numbers
85: loopne search_loop
86: mov AX,0 ; set return value to zero
87: jne number_not_found ; modify it if number found
88: mov EAX,[EBP+12] ; copy array size
89: sub EAX,ECX ; compute array index of number
90: number_not_found:
91: pop ECX ; restore registers
92: pop EBX
93: leave
94: ret 10
```

#### Program 8.2 Sorting of an integer array using the selection sort algorithm

```assembly
1: ;Sorting an array by selection sort SEL_SORT.ASM
2: ;
3: ; Objective: To sort an integer array using selection sort.
4: ; Input: Requests numbers to fill array.
5: ; Output: Displays sorted array.
6: %include "io.mac"
7:
8: MAX_SIZE EQU 100
9:
10: .DATA
11: input_prompt db "Please enter input array: "
12: db "(negative number terminates input)",0
13: out_msg db "The sorted array is:",0
14:
15: .UDATA
16: array resw MAX_SIZE
17:
18: .CODE
19: .STARTUP
20: PutStr input_prompt ; request input array
21: mov EBX,array
22: mov ECX,MAX_SIZE
23: array_loop:
24: GetInt AX ; read an array number
25: cmp AX,0 ; negative number?
26: jl exit_loop ; if so, stop reading numbers
27: mov [EBX],AX ; otherwise, copy into array
28: add EBX,2 ; increment array address
29: loop array_loop ; iterates a maximum of MAX_SIZE
30: exit_loop:
31: mov EDX,EBX ; EDX keeps the actual array size
32: sub EDX,array ; EDX = array size in bytes
33: sar EDX,1 ; divide by 2 to get array size
34: push EDX ; push array size & array pointer
35: push array
36: call selection_sort
37: PutStr out_msg ; display sorted array
38: nwln
39: mov ECX,EDX ; ECX = array size
40: mov EBX,array
41: display_loop:
42: PutInt [EBX]
43: nwln
44: add EBX,2
45: loop display_loop
46: done:
47: .EXIT
48:
49: ;-----------------------------------------------------------
50: ; This procedure receives a pointer to an array of integers
51: ; and the array size via the stack. The array is sorted by
52: ; using the selection sort. All registers are preserved.
53: ;-----------------------------------------------------------
54: %define SORT_ARRAY EBX
55: selection_sort:
56: pushad ; save registers
57: mov EBP,ESP
58: mov EBX,[EBP+36] ; copy array pointer
59: mov ECX,[EBP+40] ; copy array size
60: sub ESI,ESI ; array left of ESI is sorted
61: sort_outer_loop:
62: mov EDI,ESI
63: ; DX is used to maintain the minimum value and AX
64: ; stores the pointer to the minimum value
65: mov DX,[SORT_ARRAY+ESI] ; min. value is in DX
66: mov EAX,ESI ; EAX = pointer to min. value
67: push ECX
68: dec ECX ; size of array left of ESI
69: sort_inner_loop:
70: add EDI,2 ; move to next element
71: cmp DX,[SORT_ARRAY+EDI] ; less than min. value?
72: jle skip1 ; if not, no change to min. value
73: mov DX,[SORT_ARRAY+EDI] ; else, update min. value (DX)
74: mov EAX,EDI ; & its pointer (EAX)
75: skip1:
76: loop sort_inner_loop
77: pop ECX
78: cmp EAX,ESI ; EAX = ESI?
79: je skip2 ; if so, element at ESI is its place
80: mov EDI,EAX ; otherwise, exchange
81: mov AX,[SORT_ARRAY+ESI] ; exchange min. value
82: xchg AX,[SORT_ARRAY+EDI] ; & element at ESI
83: mov [SORT_ARRAY+ESI],AX
84: skip2:
85: add ESI,2 ; move ESI to next element
86: dec ECX
87: cmp ECX,1 ; if ECX = 1, we are done
88: jne sort_outer_loop
89: popad ; restore registers
90: ret 8
```

