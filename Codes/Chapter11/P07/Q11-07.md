
### Q11.7

Modify the program for the last exercise to perform subtraction.

#### last exercise (Q11.6)

Modify `bcdadd.asm` (Program 11.2) to receive two decimal numbers from the user instead of taking them from memory. The two numbers from the user should be read as ASCII strings using `GetStr`. The input numbers should be converted to the packed BCD representation for performing the addition as in Program 11.2. The result should be converted back to ASCII so that it can be displayed by `PutStr`.

##### Example: Multibyte Packed BCD Addition
As in the ASCII representation, when adding two multibyte packed BCD numbers, we have to use a loop that adds a pair of decimal digits in each iteration starting from the rightmost pair.  An example program that adds two 10-byte packed BCD numbers, `number1` and `number2`, is shown in Program 11.2.

##### Program 11.2 Packed BCD addition of two 10-digit numbers
```assembly
1:   ;Addition of integers in packed BCD form BCDADD.ASM
2:   ;
3:   ;        Objective: To demonstrate addition of two integers
4:   ;                   in the packed BCD representation.
5:   ;            Input: None.
6:   ;           Output: Displays the sum.
7:
8:   %define SUM_LENGTH 10
9:
10:  %include "io.mac"
11:
12:  .DATA
13:  sum_msg   db  ’The sum is: ’,0
14:  number1   db  12H,34H,56H,78H,90H
15:  number2   db  10H,98H,76H,54H,32H
16:  ASCIIsum  db  ’ ’,0 ; add NULL char.
17:
18:  .UDATA
19:  BCDsum    resb  5
20:
21:  .CODE
22:  	.STARTUP
23:  	mov ESI,4
24:  	mov ECX,5 ; loop iteration count
25:  	clc ; clear carry (we use ADC)
26:  add_loop:
27:  	mov AL,[number1+ESI]
28:  	adc AL,[number2+ESI]
29:  	daa ; ASCII adjust
30:  	mov [BCDsum+ESI],AL ; store the sum byte
31:  	dec ESI ; update index
32:  	loop add_loop
33:  	call ASCII_convert
34:  	PutStr sum_msg ; display sum
35:  	PutStr ASCIIsum
36:  	nwln
37:  	.EXIT
38: 
39:  ;-----------------------------------------------------------
40:  ; Converts the packed decimal number (5 digits) in BCDsum
41:  ; to ASCII represenation and stores it in ASCIIsum.
42:  ; All registers are preserved.
43:  ;-----------------------------------------------------------
44:  ASCII_convert:
45:  	pushad ; save registers
46:  	; ESI is used as index into ASCIIsum
47:  	mov ESI,SUM_LENGTH-1
48:  	; EDI is used as index into BCDsum
49:  	mov EDI,4
50:  	mov ECX,5 ; loop count (# of BCD digits)
51:  cnv_loop:
52:  	mov AL,[BCDsum+EDI] ; AL = BCD digit
53:  	mov AH,AL ; save the BCD digit
54:  	; convert right digit to ASCII & store in ASCIIsum
55:  	and AL,0FH
56:  	or AL,30H
57:  	mov [ASCIIsum+ESI],AL
58:  	dec ESI
59:  	mov AL,AH ; restore the BCD digit
60:  	; convert left digit to ASCII & store in ASCIIsum
61:  	shr AL,4 ; right-shift by 4 positions
62:  	or AL,30H
63:  	mov [ASCIIsum+ESI],AL
64:  	dec ESI
65:  	dec EDI ; update EDI
66:  	loop cnv_loop
67:  	popad ; restore registers
68:  	ret
```
The two numbers to be added are initialized on lines 14 and 15. The space for the sum (`BCDsum`) is reserved using `resb` on line 19.
The code is similar to that given in Program 11.1. However, since we add two decimal digits during each loop iteration, only five iterations are needed to add the 10-digit numbers. Therefore, processing numbers in packed BCD representation is faster than in ASCII representation. In any case, both representations are considerably slower in processing numbers than the binary representation.
At the end of the loop, the sum is stored in `BCDsum` as a packed BCD number. To display this number, we have to convert it to the ASCII form (an overhead that is not present in the ASCII version).
The procedure `ASCII_convert` takes `BCDsum` and converts it to equivalent ASCII string and stores it in `ASCIIsum`. For each byte read from `BCDsum`, two ASCII digits are generated. Note that the conversion from packed BCD to ASCII can be done by using only logical and shift operations. On the other hand, conversion from binary to ASCII requires a more expensive divide operation (thus increasing the conversion overhead).
