### Q5.1

The bubble sort example discussed in this chapter used a single source file. In this exercise you are asked to split the source code of this program into two modules: the main procedure in one module, and the bubble sort procedure in the other. Then assemble and link this code to produce the .exe file. Verify the correctness of the program.

####  Example 5.5 *Bubble sort procedure*.

There are several algorithms to sort an array of numbers. The algorithm we use here is called the *bubble sort* algorithm. We assume that the array is to be sorted in ascending order. The bubble sort algorithm consists of several passes through the array. Each pass scans the array, performing the following actions:
```
• Compare adjacent pairs of data elements;
• If they are out of order, swap them.
```
The algorithm terminates if, during a pass, no data elements are swapped. Even if a single swap is done during a pass, it will initiate another pass to scan the array.
	Figure 5.8 shows the behavior of the algorithm during the first pass. The algorithm starts by comparing the first and second data elements (4 and 3). Since they are out of order, 4 and 3 are interchanged. Next, the second data element 4 is compared with the third data element 5, and no swapping takes place as they are in order. During the next step, 5 and 1 are compared and swapped and finally 5 and 2 are swapped. This terminates the first pass. The algorithm has performed $N − 1$ comparisons, where $N$ is the number of data elements in the array. At the end of the first pass, the largest data element 5 is moved to its final position in the array.
	Figure 5.9 shows the state of the array after each pass. Notice that after the first pass, the largest number (5) is in its final position. Similarly, after the second pass, the second largest number (4) moves to its final position, and so on. This is why this algorithm is called the bubble sort: during the first pass, the largest element bubbles to the top, the second largest bubbles to the top during the second pass, and so on. Even though the array is in sorted order after the third pass, one more pass is required by the algorithm to detect this.
```
bubble_sort (arrayPointer, arraySize)
	status := UNSORTED
	#comparisons := arraySize
	while (status = UNSORTED)
		#comparisons := #comparisons − 1
		status := SORTED
		for (i = 0 to #comparisons)
			if (array[i] > array[i+1])
				swap ith and (i + 1)th elements of the array
				status := UNSORTED
			end if
		end for
	end while
end bubble_sort
```
**Figure 5.10** Pseudocode for the bubble sort algorithm.

The number of passes required to sort an array depends on how unsorted the initial array is. If the array is in sorted order, only a single pass is required. At the other extreme, if the array is completely unsorted (i.e., elements are initially in the descending order), the algorithm requires the maximum number of passes equal to one less than the number of elements in the array. The pseudocode for the bubble sort algorithm is shown in Figure 5.10.
	The bubble sort program (Program 5.5) requests a set of up to 20 nonzero integers from the user and displays them in sorted order. The input can be terminated earlier by typing a zero.
	The logic of the main program is straightforward. The `read_loop` (lines 25 to 32) reads the input integers. Since the ECX register is initialized to `MAX_SIZE`, which is set to 20 in this program, the `read_loop` iterates a maximum of 20 times. Typing a zero can also terminate the loop. The zero input condition is detected and the loop is terminated by the statements on lines 27 and 28.
	The `bubble_sort` procedure receives the array size and a pointer to the array. These two parameters are pushed onto the stack (lines 34 and 35) before calling the `bubble_sort` procedure. The `print_loop` (lines 41 to 45) displays the sorted array.
	In the `bubble-sort` procedure, the ECX register is used to keep track of the number of comparisons while EDX maintains the status information. The ESI register points to the $i$th element of the input array.
	The `while` loop condition is tested by lines 91 to 93. The `for` loop body corresponds to lines 80 to 89 and 97 to 100. The rest of the code follows the pseudocode. Note that the array pointer is available in the stack at EBP + 36 and its size at EBP + 40, as we use `pushad` to save all registers.

##### **Program 5.5** Bubble sort program to sort integers in ascending order

```assembly
 1:   ;Bubble sort procedure BBLSORT.ASM
 2:   ; 	Objective: To implement the bubble sort algorithm.
 3:   ; 		Input: A set of nonzero integers to be sorted.
 4:   ; 			   Input is terminated by entering zero.
 5:   ; 	   Output: Outputs the numbers in ascending order.
 6:
 7:   %define	CRLF	0DH, 0AH
 8:   MAX_SIZE	EQU		20
 9:   %include "io.mac"
10:   .DATA
11:   prompt_msg	db	"Enter nonzero integers to be sorted.", CRLF
12:   				db	"Enter zero to terminate the input.", 0
13:   output_msg 	db	"Input numbers in ascending order:", 0
14:
15:   .UDATA
16:   array		resd	MAX_SIZE ; input array for integers
17:
18:   .CODE
19:   .STARTUP
20:   PutStr prompt_msg		; request input numbers
21:   nwln
22:   mov EBX, array 		; EBX = array pointer
23:   mov ECX, MAX_SIZE 	; ECX = array size
24:   sub EDX, EDX	 		; number count = 0
25:   read_loop:
26:   GetLInt EAX 			; read input number
27:   cmp EAX, 0 			; if the number is zero
28:   je stop_reading 		; no more numbers to read
29:   mov [EBX], EAX 		; copy the number into array
30:   add EBX, 4 			; EBX points to the next element
31:   inc EDX 				; increment number count
32:   loop read_loop 		; reads a max. of MAX_SIZE numbers
33:   stop_reading:
34:   push EDX 				; push array size onto stack
35:   push array 			; place array pointer on stack
36:   call bubble_sort
37:   PutStr output_msg 	; display sorted input numbers
38:   nwln
39:   mov EBX, array
40:   mov ECX, EDX 			; ECX = number count
41:   print_loop:
42:   PutLInt [EBX]
43:   nwln
44:   add EBX, 4
45:   loop print_loop
46:   done:
47:   .EXIT
48:   ;-----------------------------------------------------------
49:   ; This procedure receives a pointer to an array of integers
50:   ; and the size of the array via the stack. It sorts the
51:   ; array in ascending order using the bubble sort algorithm.
52:   ;-----------------------------------------------------------
53:   SORTED EQU 0
54:   UNSORTED EQU 1
55:   bubble_sort:
56:   pushad
57:   mov EBP, ESP
58:
59:   ; ECX serves the same purpose as the end_index variable
60:   ; in the C procedure. ECX keeps the number of comparisons
61:   ; to be done in each pass. Note that ECX is decremented
62:   ; by 1 after each pass.
63:   mov ECX, [EBP+40] ; load array size into ECX
64:
65:   next_pass:
66:   dec ECX ; if # of comparisons is zero
67:   jz sort_done ; then we are done
68:   mov EDI,ECX ; else start another pass
69:
70:   ; DL is used to keep SORTED/UNSORTED status
71:   mov DL, SORTED ; set status to SORTED
72:
73:   mov ESI, [EBP+36] ; load array address into ESI
74:   ; ESI points to element i and ESI+4 to the next element
75:   pass:
76:   ; This loop represents one pass of the algorithm.
77:   ; Each iteration compares elements at [ESI] and [ESI+4]
78:   ; and swaps them if ([ESI]) < ([ESI+4]).
79:
80:   mov EAX, [ESI]
81:   mov EBX, [ESI+4]
82:   cmp EAX, EBX
83:   jg swap
84:
85:   increment:
86:   ; Increment ESI by 4 to point to the next element
87:   add ESI,4
88:   dec EDI
89:   jnz pass
90:
91:   cmp EDX,SORTED ; if status remains SORTED
92:   je sort_done ; then sorting is done
93:   jmp next_pass ; else initiate another pass
94:
95: swap:
96:   ; swap elements at [ESI] and [ESI+4]
97:   mov [ESI+4],EAX ; copy [ESI] in EAX to [ESI+4]
98:   mov [ESI],EBX ; copy [ESI+4] in EBX to [ESI]
99:   mov EDX,UNSORTED ; set status to UNSORTED
100:  jmp increment
101:
102:  sort_done:
103:  popad
104:  ret 8
```